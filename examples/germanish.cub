type req = Empty | Reqs | Reqe
type cstate = Invalid | Shared | Exclusive

globals = Exgntd[bool] Curcmd[req] Curptr[proc]
arrays = Cache[proc,cstate] Shrset[proc,bool]

init (z) { Cache[z] = Invalid && Shrset[z] = False &&
           Exgntd = False && Curcmd = Empty }

unsafe (z1 z2) { Cache[z1] = Exclusive && Cache[z2] = Shared }

transition req_shared (n)
require { Curcmd = Empty && Cache[n] = Invalid }
assign { Curcmd := Reqs; Curptr := n }
    
transition req_exclusive (n)
require { Curcmd = Empty && Cache[n] <> Exclusive }
assign { Curcmd := Reqe; Curptr := n }
    
transition inv_1 (n)
require { Shrset[n]=True  &&  Curcmd = Reqe }
assign { Exgntd := False }
Cache[j] := {| j = n : Invalid | _ : Cache[j] }
Shrset[j] := {| j= n : False | _ : Shrset[j] }

transition inv_2 (n)
require { Shrset[n]=True  && Curcmd = Reqs && Exgntd=True }
assign { Exgntd := False }
Cache[j] := {| j = n : Invalid | _ : Cache[j] }
Shrset[j] := {| j= n : False | _ : Shrset[j] }
    
transition gnt_shared (n)
require { Curptr = n && Curcmd = Reqs && Exgntd = False }
assign { Curcmd := Empty }
Shrset[j] := {| j = n : True | _ : Shrset[j] }
Cache[j] := {| j = n : Shared | _ : Cache[j] }

transition gnt_exclusive (n)
require { Shrset[n] = False && Curcmd = Reqe && Exgntd = False && Curptr = n}
uguard (l) { Shrset[l] = False }
assign { Curcmd := Empty; Exgntd := True }
Shrset[j] := {| j = n : True | _ : Shrset[j] }
Cache[j] := {| j = n : Exclusive | _ : Cache[j] }
