type loc = L1 | L2 | Wait | Crit | L3

array PC[proc] : loc
array Want[proc] : bool

var Turn : proc
var P1 : proc
var P2 : proc

init () { P1 <> P2 && PC[P1] = L1 && PC[P2] = L1 && 
          Want[P1] = False &&  Want[P2] = False }

unsafe (z) { PC[P1] = Crit && PC[P2] = Crit } 

(* proc 1 *)

transition req_P1 ()
requires { PC[P1] = L1 }
{ 
  Want[j] := case | j = P1 : True | _ : Want[j];
  PC[j] := case | j = P1 : L2 | _ : PC[j] }


transition myturn_P1 ()
requires { PC[P1] = L2 }
{ 
  Turn := P1;
  PC[j] := case | j = P1 : Wait | _ : PC[j] }

transition enter1_P1 ()
requires { PC[P1] = Wait && Want[P2] = False }
{ PC[j] := case | j = P1 : Crit | _ : PC[j] }  


transition enter2_P1 ()
requires { PC[P1] = Wait && Turn = P2  }
{ PC[j] := case | j = P1 : Crit | _ : PC[j] }  


transition exit_P1 ()
requires { PC[P1] = Crit }
{ 
  PC[j] := case | j = P1 : L3 | _ : PC[j] }  

transition not_want_P1 ()
requires { PC[P1] = L3 }
{ 
  Want[j] := case | j = P1 : False | _ : Want[j];
  PC[j] := case | j = P1 : L1 | _ : PC[j]
}


(* proc 2 *)

transition req_P2 ()
requires { PC[P2] = L1 }
{ 
  Want[j] := case | j = P2 : True | _ : Want[j];
  PC[j] := case | j = P2 : L2 | _ : PC[j] }

transition myturn_P2 ()
requires { PC[P2] = L2 }
{ 
  Turn := P2;
  PC[j] := case | j = P2 : Wait | _ : PC[j] }

transition enter1_P2 ()
requires { PC[P2] = Wait && Want[P1] = False }
{ PC[j] := case | j = P2 : Crit | _ : PC[j] }  

transition enter2_P2 ()
requires { PC[P2] = Wait && Turn = P1 }
{ PC[j] := case | j = P2 : Crit | _ : PC[j] }  

transition exit_P2 ()
requires { PC[P2] = Crit }
{ 
  PC[j] := case | j = P2 : L3 | _ : PC[j] }  

transition not_want_P2 ()
requires { PC[P2] = L3 }
{ 
  Want[j] := case | j = P2 : False | _ : Want[j];
  PC[j] := case | j = P2 : L1 | _ : PC[j]
}
