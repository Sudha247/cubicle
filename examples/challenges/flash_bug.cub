type state = Invalid | Shared | Exclusive
type msg = Empty | Get | GetX | Nak | Put | PutX
type wait = None | WGet | WGetX
type inv = Empty_inv | Inv | InvAck
type wb_msg = Empty_wb | WB
type shwb_msg = Empty_shwb | ShWB | FAck
type data


var Home : proc
var Memory : data
var Pending : bool
var Local : bool
var Dirty : bool
var Head_valid : bool
var Head_pointer : proc

var Nakc :bool

var WBnet_mtype : wb_msg
var WBnet_data : data
var WBnet_proc : proc

var ShWBnet_mtype : shwb_msg
var ShWBnet_data : data
var ShWBnet_proc : proc

array CacheState[proc] : state
array CacheWait[proc] : wait
array CacheData[proc] : data
array CacheInvmarked[proc] : bool

array UnetProc[proc] : proc
array UnetMtype[proc] : msg
array UnetData[proc] : data

array Invnet[proc] : inv
array Shlist[proc] : bool
array RP[proc] : bool
array Real[proc] : bool



init (p) {
     Pending = False &&
     Local = False &&
     Dirty = False &&
     Head_valid = False &&

     Nakc = False &&

     WBnet_mtype = Empty_wb &&

     ShWBnet_mtype = Empty_shwb &&

     CacheState[p] = Invalid &&
     CacheWait[p] = None &&
     CacheInvmarked[p] = False &&

     UnetMtype[p] = Empty &&

     Invnet[p] = Empty_inv &&
     Shlist[p] = False &&
     Real[p] = False &&
     RP[p] = False
}



unsafe (p1 p2) { Home = p1 && CacheState[p1] = Exclusive && CacheState[p2] = Exclusive }



transition pi_Local_Get_Dirty ()
require { CacheState[Home] = Invalid && CacheWait[Home] = None && 
	  Pending = False && Dirty = True }
assign { Pending := True }
CacheWait[j] := {| j = Home : WGet | _ : CacheWait[j] }
UnetProc[j] := {| j = Home : Head_pointer | _ : UnetProc[j] }
UnetMtype[j] := {| j = Home : Get | _ : UnetMtype[j] }
(* UnetData[j] := {| j = Home : Undefined | _ : UnetData[j] } *)

transition pi_Local_Get_Other1 ()
require { CacheState[Home] = Invalid && CacheWait[Home] = None && 
	  Pending = False && Dirty = False && CacheInvmarked[Home] = True }
assign { Local := True }
CacheWait[j] := {| j = Home : None | _ : CacheWait[j] }
CacheInvmarked[j] := {| j = Home : False | _ : CacheInvmarked[j] }
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }

transition pi_Local_Get_Other2 ()
require { CacheState[Home] = Invalid && CacheWait[Home] = None && 
	  Pending = False && Dirty = False && CacheInvmarked[Home] = False }
assign { Local := True }
CacheWait[j] := {| j = Home : None | _ : CacheWait[j] }
CacheData[j] := {| j = Home : Memory | _ : CacheData[j] }
CacheState[j] := {| j = Home : Shared | _ : CacheState[j] }



transition pi_Remote_Get (src)
require { Home <> src &&
	  CacheState[src] = Invalid && CacheWait[src] = None }
CacheWait[j] := {| j = src : WGet | _ : CacheWait[j] }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Get | _ : UnetMtype[j] }
(* UnetData[j] := {| j = src : Undefined | _ : UnetData[j] } *)



transition pi_Local_GetX_Dirty ()
require { CacheState[Home] <> Exclusive && CacheWait[Home] = None &&
	  Pending = False && Dirty = True }
assign { Pending := True }
CacheWait[j] := {| j = Home : WGetX | _ : CacheWait[j] }
UnetProc[j] := {| j = Home : Head_pointer | _ : UnetProc[j] }
UnetMtype[j] := {| j = Home : GetX | _ : UnetMtype[j] }
(* UnetData[j] := {| j = Home : Undefined | _ : UnetData[j] } *)

transition pi_Local_GetX_Other ()
require { Head_valid = False &&
	  CacheState[Home] <> Exclusive && CacheWait[Home] = None && 
	  Pending = False && Dirty = False }
assign { Local := True; Dirty := True }
CacheData[j] := {| j = Home : Memory | _ : CacheData[j] }
CacheWait[j] := {| j = Home : None | _ : CacheWait[j] }
CacheInvmarked[j] := {| j = Home : False | _ : CacheInvmarked[j] }
CacheState[j] := {| j = Home : Exclusive | _ : CacheState[j] }

transition pi_Local_GetX_Other_head ()
require { Head_valid = True &&
	  CacheState[Home] <> Exclusive && CacheWait[Home] = None && 
	  Pending = False && Dirty = False }
assign { Local := True; Dirty := True;
	 Pending := True;
	 Head_valid := False }
CacheData[j] := {| j = Home : Memory | _ : CacheData[j] }
CacheWait[j] := {| j = Home : None | _ : CacheWait[j] }
CacheInvmarked[j] := {| j = Home : False | _ : CacheInvmarked[j] }
CacheState[j] := {| j = Home : Exclusive | _ : CacheState[j] }
Shlist[j] := {| _ : False }
Invnet[j] := {| j = Home : Empty_inv
	      | j = Head_pointer : Inv
	      | Shlist[j] = True : Inv
	      | _ : Empty_inv }
Real[j] := {| j = Home : False 
	    | j = Head_pointer : True 
	    | _ : Shlist[j] }




transition pi_Remote_GetX (src)
require { Home <> src &&
	  CacheState[src] = Invalid && CacheWait[src] = None }
CacheWait[j] := {| j = src : WGetX | _ : CacheWait[j] }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : GetX | _ : UnetMtype[j] }
(* UnetData[j] := {| j = src : Undefined | _ : UnetData[j] } *)



transition pi_Local_PutX_Pending ()
require { CacheState[Home] = Exclusive && CacheWait[Home] = None &&
	  Pending = True}
assign { Dirty := False; Memory := CacheData[Home] }
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }

transition pi_Local_PutX_Other ()
require { CacheState[Home] = Exclusive && CacheWait[Home] = None &&
	  Pending = True}
assign { Local := False; Dirty := False; Memory := CacheData[Home] }
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }


transition pi_Remote_PutX (dst)
require { Home <> dst && CacheState[dst] = Exclusive }
assign { WBnet_proc := dst; WBnet_mtype := WB; WBnet_data := CacheData[dst] }
CacheState[j] := {| j = dst : Invalid | _ : CacheState[j] }



transition pi_Local_Replace ()
require { CacheState[Home] = Shared && CacheWait[Home] = None }
assign { Local := False }
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }



transition pi_Remote_Replace (src)
require { Home <> src && CacheState[src] = Shared && CacheWait[src] = None }
CacheState[j] := {| j = src : Invalid | _ : CacheState[j] }
RP[j] := {| j = src : True | _ : RP[j] }



transition ni_NAK (dst)
require { UnetMtype[dst] = Nak }
UnetMtype[j] := {| j = dst : Empty | _ : UnetMtype[j] }
CacheWait[j] := {| j = dst : None | _ : CacheWait[j] }
CacheInvmarked[j] := {| j = dst : False | _ : CacheInvmarked[j] }



transition ni_NAK_Clear ()
require { Nakc = True }
assign { Pending := False; Nakc := False }



transition ni_Local_Get_Pending (src)
require { UnetMtype[src] = Get && Home <> src && 
	  UnetProc[src] = Home && RP[src] = False &&
	  Pending = True }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Nak | _ : UnetMtype[j] }
(* UnetData[j] := {| j = src : Undefined | _ : UnetData[j] } *)

transition ni_Local_Get_Dirty_NotLocal (src)
require { UnetMtype[src] = Get && Home <> src && 
	  UnetProc[src] = Home && RP[src] = False &&
	  Pending = False && Dirty = True && Local = False }
assign { Pending := True }
UnetProc[j] := {| j = src : Head_pointer | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Get | _ : UnetMtype[j] }
(* UnetData[j] := {| j = src : Undefined | _ : UnetData[j] } *)

transition ni_Local_Get_Dirty_Local (src)
require { UnetMtype[src] = Get && Home <> src && 
	  UnetProc[src] = Home && RP[src] = False &&
	  Pending = False && Dirty = True && Local = True }
assign { Dirty := False; Head_valid := True; Head_pointer := src;
         Memory := CacheData[Home] }
CacheState[j] := {| j = Home : Shared | _ : CacheState[j] }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Put | _ : UnetMtype[j] }
UnetData[j] := {| j = src : CacheData[Home] | _ : UnetData[j] }

transition ni_Local_Get_NotDirty_Head (src)
require { UnetMtype[src] = Get && Home <> src && 
	  UnetProc[src] = Home && RP[src] = False &&
	  Pending = False && Dirty = False && Head_valid = True }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Put | _ : UnetMtype[j] }
UnetData[j] := {| j = src : Memory | _ : UnetData[j] }
Shlist[j] := {| j = src : True | _ : Shlist[j] }
Real[j] := {| j = src : True | _ : Shlist[j] }

transition ni_Local_Get_NotDirty (src)
require { UnetMtype[src] = Get && Home <> src && 
	  UnetProc[src] = Home && RP[src] = False &&
	  Pending = False && Dirty = False && Head_valid = False }
assign { Head_valid := True; Head_pointer := src }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Put | _ : UnetMtype[j] }
UnetData[j] := {| j = src : Memory | _ : UnetData[j] }



transition ni_Remote_Get1 (src dst)
require { UnetMtype[src] = Get &&
	  UnetProc[src] = dst && Home <> dst && 
	  CacheState[dst] <> Exclusive }
assign { Nakc := True }
UnetProc[j] := {| j = src : dst | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Nak | _ : UnetMtype[j] }
(* UnetData[j] := {| j = src : Undefined | _ : UnetData[j] } *)

transition ni_Remote_Get2 (dst)
require { UnetMtype[Home] = Get &&
	  UnetProc[Home] = dst && Home <> dst &&
	  CacheState[dst] = Exclusive && CacheInvmarked[Home] = False }
CacheState[j] := {| j = dst : Shared | _ : CacheState[j] }
UnetProc[j] := {| j = Home : dst | _ : UnetProc[j] }
UnetMtype[j] := {| j = Home : Put | _ : UnetMtype[j] }
UnetData[j] := {| j = Home : CacheData[dst] | _ : UnetData[j] }

transition ni_Remote_Get3 (src dst)
require { UnetMtype[src] = Get &&
	  UnetProc[src] = dst && Home <> dst &&
	  CacheState[dst] = Exclusive && CacheInvmarked[src] = False &&
	  Home <> src }
assign { ShWBnet_proc := src;
	 ShWBnet_mtype := ShWB;
	 ShWBnet_data := CacheData[dst] }
CacheState[j] := {| j = dst : Shared | _ : CacheState[j] }
UnetProc[j] := {| j = src : dst | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Put | _ : UnetMtype[j] }
UnetData[j] := {| j = src : CacheData[dst] | _ : UnetData[j] }





transition ni_Local_GetX_Pending (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = True }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Nak | _ : UnetMtype[j] }
(* UnetData[j] := {| j = src : Undefined | _ : UnetData[j] } *)

transition ni_Local_GetX_Dirty_NotLocal (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = False && Dirty = True && Local = False }
assign { Pending := True }
UnetProc[j] := {| j = src : Head_pointer | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : GetX | _ : UnetMtype[j] }
(* UnetData[j] := {| j = src : Undefined | _ : UnetData[j] } *)

transition ni_Local_GetX_Dirty_Local (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = False && Dirty = True && Local = True }
assign { Local := False; Dirty := True; Head_valid := True; 
         Head_pointer := src } 
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : PutX | _ : UnetMtype[j] }
UnetData[j] := {| j = src : CacheData[Home] | _ : UnetData[j] }
Shlist[j] := {| _ : False }
Real[j] := {| _ : False }

transition ni_Local_GetX_NotDirty_NotLocal (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = False && Dirty = False && Local = False &&
	  Head_valid = False }
assign { Local := False; Dirty := True; Head_valid := True; 
         Head_pointer := src } 
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : PutX | _ : UnetMtype[j] }
UnetData[j] := {| j = src : Memory | _ : UnetData[j] }
Shlist[j] := {| _ : False }
Real[j] := {| _ : False }

transition ni_Local_GetX_NotDirty_Local1 (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = False && Dirty = False && Local = True &&
	  Head_valid = False && CacheWait[Home] = WGet }
assign { Local := False; Dirty := True; Head_valid := True; 
         Head_pointer := src } 
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : PutX | _ : UnetMtype[j] }
UnetData[j] := {| j = src : Memory | _ : UnetData[j] }
Shlist[j] := {| _ : False }
Real[j] := {| _ : False }
CacheInvmarked[j] := {| j = Home : True | _ : CacheInvmarked[j] }

transition ni_Local_GetX_NotDirty_Local2 (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = False && Dirty = False && Local = True &&
	  Head_valid = False && CacheWait[Home] <> WGet }
assign { Local := False; Dirty := True; Head_valid := True; 
         Head_pointer := src } 
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : PutX | _ : UnetMtype[j] }
UnetData[j] := {| j = src : Memory | _ : UnetData[j] }
Shlist[j] := {| _ : False }
Real[j] := {| _ : False }

transition ni_Local_GetX_NotDirty_Head (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = False && Dirty = False &&
	  Head_valid = True }
assign { Local := False; Dirty := True; Head_valid := True; 
         Head_pointer := src; Pending := True }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : PutX | _ : UnetMtype[j] }
UnetData[j] := {| j = src : Memory | _ : UnetData[j] }
Shlist[j] := {| _ : False }
Real[j] := {| j = Home : False
	    | j = src : False
	    | j = Head_pointer : True 
	    | _ : Shlist[j] }
Invnet[j] := {| j = Home : Empty_inv
	      | j = src : Empty_inv
	      | j = Head_pointer : Inv
	      | Shlist[j] = True : Inv
	      | _ : Empty_inv }

transition ni_Local_GetX_NotDirty_Head_Local1 (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = False && Dirty = False &&
	  Head_valid = True && Local = True && CacheWait[Home] = WGet}
assign { Local := False; Dirty := True; Head_valid := True; 
         Head_pointer := src; Pending := True }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : PutX | _ : UnetMtype[j] }
UnetData[j] := {| j = src : Memory | _ : UnetData[j] }
Shlist[j] := {| _ : False }
Real[j] := {| j = Home : False
	    | j = src : False
	    | j = Head_pointer : True 
	    | _ : Shlist[j] }
Invnet[j] := {| j = Home : Empty_inv
	      | j = src : Empty_inv
	      | j = Head_pointer : Inv
	      | Shlist[j] = True : Inv
	      | _ : Empty_inv }
CacheInvmarked[j] := {| j = Home : True | _ : CacheInvmarked[j] }
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }

transition ni_Local_GetX_NotDirty_Head_Local2 (src)
require { UnetMtype[src] = GetX && Home <> src && UnetProc[src] = Home &&
	  Pending = False && Dirty = False &&
	  Head_valid = True && Local = True && CacheWait[Home] <> WGet}
assign { Local := False; Dirty := True; Head_valid := True; 
         Head_pointer := src; Pending := True }
UnetProc[j] := {| j = src : Home | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : PutX | _ : UnetMtype[j] }
UnetData[j] := {| j = src : Memory | _ : UnetData[j] }
Shlist[j] := {| _ : False }
Real[j] := {| j = Home : False
	    | j = src : False
	    | j = Head_pointer : True 
	    | _ : Shlist[j] }
Invnet[j] := {| j = Home : Empty_inv
	      | j = src : Empty_inv
	      | j = Head_pointer : Inv
	      | Shlist[j] = True : Inv
	      | _ : Empty_inv }
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }





transition ni_Remote_GetX1 (src dst)
require { UnetMtype[src] = GetX &&
	  UnetProc[src] = dst && Home <> dst && 
	  CacheState[dst] <> Exclusive }
assign { Nakc := True }
UnetProc[j] := {| j = src : dst | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Nak | _ : UnetMtype[j] }
(* UnetData[j] := {| j = src : Undefined | _ : UnetData[j] } *)

transition ni_Remote_GetX2 (dst)
require { UnetMtype[Home] = GetX &&
	  UnetProc[Home] = dst && Home <> dst &&
	  CacheState[dst] = Exclusive }
CacheState[j] := {| j = dst : Invalid | _ : CacheState[j] }
UnetProc[j] := {| j = Home : dst | _ : UnetProc[j] }
UnetMtype[j] := {| j = Home : PutX | _ : UnetMtype[j] }
UnetData[j] := {| j = Home : CacheData[dst] | _ : UnetData[j] }

transition ni_Remote_GetX3 (src dst)
require { UnetMtype[src] = GetX &&
	  UnetProc[src] = dst && Home <> dst &&
	  CacheState[dst] = Exclusive && Home <> src }
assign { ShWBnet_proc := src;
	 ShWBnet_mtype := FAck
	 (* ShWBnet_data := Undefined *) }
CacheState[j] := {| j = dst : Invalid | _ : CacheState[j] }
UnetProc[j] := {| j = src : dst | _ : UnetProc[j] }
UnetMtype[j] := {| j = src : Put | _ : UnetMtype[j] }
UnetData[j] := {| j = src : CacheData[dst] | _ : UnetData[j] }





transition ni_Local_Put1 ()
require { UnetMtype[Home] = Put &&
	  CacheInvmarked[Home] = True }
assign { Pending := False; Dirty := False; Local := True;
         Memory := UnetData[Home] }
UnetMtype[j] := {| j = Home : Empty | _ : UnetMtype[j] }
CacheWait[j] := {| j = Home : None | _ : CacheWait[j] }
CacheInvmarked[j] := {| j = Home : False | _ : CacheInvmarked[j] }
CacheState[j] := {| j = Home : Invalid | _ : CacheState[j] }

transition ni_Local_Put2 ()
require { UnetMtype[Home] = Put &&
	  CacheInvmarked[Home] = False }
assign { Pending := False; Dirty := False; Local := True;
         Memory := UnetData[Home] }
UnetMtype[j] := {| j = Home : Empty | _ : UnetMtype[j] }
CacheWait[j] := {| j = Home : None | _ : CacheWait[j] }
CacheData[j] := {| j = Home : UnetData[Home] | _ : CacheData[j] }
CacheState[j] := {| j = Home : Shared | _ : CacheState[j] }





transition ni_Remote_Put1 (dst)
require { Home <> dst && UnetMtype[dst] = Put &&
	  CacheInvmarked[dst] = True }
UnetMtype[j] := {| j = dst : Empty | _ : UnetMtype[j] }
CacheWait[j] := {| j = dst : None | _ : CacheWait[j] }
CacheInvmarked[j] := {| j = dst : False | _ : CacheInvmarked[j] }
CacheState[j] := {| j = dst : Invalid | _ : CacheState[j] }

transition ni_Remote_Put2 (dst)
require { Home <> dst && UnetMtype[dst] = Put &&
	  CacheInvmarked[dst] = False }
UnetMtype[j] := {| j = dst : Empty | _ : UnetMtype[j] }
CacheWait[j] := {| j = dst : None | _ : CacheWait[j] }
CacheData[j] := {| j = dst : UnetData[dst] | _ : CacheData[j] }
CacheState[j] := {| j = dst : Shared | _ : CacheState[j] }





transition ni_Local_PutXAcksDone ()
require { UnetMtype[Home] = PutX }
assign { Pending := False; Head_valid := False; Local := True }
UnetMtype[j] := {| j = Home : Empty | _ : UnetMtype[j] }
CacheData[j] := {| j = Home : UnetData[Home] | _ : CacheData[j] }
CacheWait[j] := {| j = Home : None | _ : CacheWait[j] }
CacheInvmarked[j] := {| j = Home : False | _ : CacheInvmarked[j] }
CacheState[j] := {| j = Home : Exclusive | _ : CacheState[j] }





transition ni_Remote_PutX (dst)
require { Home <> dst && UnetMtype[dst] = PutX &&
	  CacheWait[dst] = WGetX }
UnetMtype[j] := {| j = dst : Empty | _ : UnetMtype[j] }
CacheData[j] := {| j = dst : UnetData[dst] | _ : CacheData[j] }
CacheWait[j] := {| j = dst : None | _ : CacheWait[j] }
CacheInvmarked[j] := {| j = dst : False | _ : CacheInvmarked[j] }
CacheState[j] := {| j = dst : Exclusive | _ : CacheState[j] }





transition ni_Inv1 (dst)
require { Home <> dst && Invnet[dst] = Inv &&
	  CacheWait[dst] = WGet }
Invnet[j] := {| j = dst : InvAck | _ : Invnet[j] }
CacheInvmarked[j] := {| j = dst : True | _ : CacheInvmarked[j] }
CacheState[j] := {| j = dst : Invalid | _ : CacheState[j] }

transition ni_Inv2 (dst)
require { Home <> dst && Invnet[dst] = Inv &&
	  CacheWait[dst] <> WGet }
Invnet[j] := {| j = dst : InvAck | _ : Invnet[j] }
CacheState[j] := {| j = dst : Invalid | _ : CacheState[j] }





transition ni_InvAck1 (src)
require { Home <> src && Pending = True && 
	  Invnet[src] = InvAck && Real[src] = True }
Invnet[j] := {| j = src : Empty_inv | _ : Invnet[j] }
Real[j] := {| j = src : False | _ : Real[j] }

transition ni_InvAck2 (src)
require { Home <> src && Pending = True && 
	  Invnet[src] = InvAck && Real[src] = True && 
	  Dirty = False }
uguard (j) { Real[j] = False }
assign { Pending := False; Local := False }
Invnet[j] := {| j = src : Empty_inv | _ : Invnet[j] }
Real[j] := {| j = src : False | _ : Real[j] }

transition ni_InvAck3 (src)
require { Home <> src && Pending = True && 
	  Invnet[src] = InvAck && Real[src] = True && 
	  Dirty = True }
uguard (j) { Real[j] = False }
assign { Pending := False }
Invnet[j] := {| j = src : Empty_inv | _ : Invnet[j] }
Real[j] := {| j = src : False | _ : Real[j] }





transition ni_WB ()
require { WBnet_mtype = WB }
assign { Dirty := False; Head_valid := False; Memory := WBnet_data;
         WBnet_mtype := Empty_wb }





transition ni_FAck1 ()
require { ShWBnet_mtype = FAck && Dirty = False }
assign { ShWBnet_mtype := Empty_shwb; Pending := False }

transition ni_FAck2 ()
require { ShWBnet_mtype = FAck && Dirty = True }
assign { ShWBnet_mtype := Empty_shwb; Pending := False; 
         Head_pointer := ShWBnet_proc }






transition ni_ShWB ()
require { ShWBnet_mtype = ShWB }
assign { ShWBnet_mtype := Empty_shwb; Pending := False;
         Dirty := False; Memory := ShWBnet_data }
Shlist[j] := {| j = ShWBnet_proc : True | _ : Shlist[j] }
Real[j] := {| j = ShWBnet_proc : True | _ : Shlist[j] }





transition ni_Replace (src)
require { RP[src] = True }
Shlist[j] := {| j = src : False | _ : Shlist[j] }
Real[j] := {| j = src : False | _ : Real[j] }
