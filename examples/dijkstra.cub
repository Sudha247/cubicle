type location = L1 | L2 | L3 | L4 | L5 | L6 | L7

array A[proc] : location
array F[proc] : int
array P[proc] : bool

init (x) { A[x] = L1 && P[x] = False && F[x] = 0 }

unsafe (z1 z2) { A[z1] = L6 && A[z2] = L6 }
  
transition t1 (x)
requires { A[x] = L1 }
{ A[j] := case | j = x : L2 | _ : A[j];
  F[j] := case | j = x : 1 | _ : F[j]; }


transition t2a (x)
requires { A[x] = L2 && forall_other j.  P[j] = False }
{ A[j] := case | j = x : L3 | _ : A[j]; }

transition t2b (x)
requires { A[x] = L2 && forall_other j. (  P[j] = True && F[j] = 0 ) }
{ A[j] := case | j = x : L3 | _ : A[j]; }

transition t3 (x y)
requires { A[x] = L3 && P[y] = True }
{ A[j] := case | j = x : L4 | _ : A[j];
  P[j] := case | j = y : True | _ : P[j]; }

transition t4 (x)
requires { A[x] = L4 }
{ A[j] := case | j = x : L5 | _ : A[j];
  F[j] := case | j = x : 2 | _ : F[j]; }

transition t5 (x)
requires { A[x] = L5 && forall_other j.  F[j] <> 2 }
{ A[j] := case | j = x : L6 | _ : A[j]; }

transition t6 (x y)
requires { A[x] = L5 && F[y] = 2 }
{ A[j] := case | j = x : L1 | _ : A[j]; }

transition t7 (x)
requires { A[x] = L6 }
{ A[j] := case | j = x : L7 | _ : A[j]; 
  F[j] := case | j = x : 0 | _ : F[j]; }

transition t8 (x)
requires { A[x] = L7 }
{ A[j] := case | j = x : L1 | _ : A[j]; }
