(*
 German cache coherence protocol as modeled by Ching-Tsun Chou and used in the
 paper C.-T. Chou, P. K. Mannava, and S. Park, A simple method for parameterized
 verification of cache coherence protocols, FMCAD 2004.

 Version without data paths
*)

type state = Invalid | Shared | Exclusive
type msg = Empty | Reqs | Reqe | Inv | Invack | Gnts | Gnte

var Exgntd : bool
var Curcmd : msg
var CurClient : proc

array Chan1Cmd[proc] : msg
array Chan2Cmd[proc] : msg
array Chan3Cmd[proc] : msg
array CacheState[proc] : state

array Invset[proc] : bool
array Shrset[proc] : bool

init (z) { 		   
     Chan1Cmd[z] = Empty &&
     Chan2Cmd[z] = Empty &&
     Chan3Cmd[z] = Empty &&
     CacheState[z] = Invalid &&
     Invset[z] = False &&
     Shrset[z] = False &&
     Curcmd = Empty &&
     Exgntd = False 
     }


(* Control *)
unsafe (z1 z2) { CacheState[z1] = Exclusive && CacheState[z2] <> Invalid }


transition sendReqs (i)
requires { Chan1Cmd[i] = Empty && CacheState[i] = Invalid }
{
  Chan1Cmd[i] := Reqs;
}

transition sendReqe (i)
requires { Chan1Cmd[i] = Empty && CacheState[i] <> Exclusive }
{
  Chan1Cmd[i] := Reqe;
}


transition recvReqs (i)
requires { Curcmd = Empty && Chan1Cmd[i] = Reqs }
{
  Curcmd := Reqs;
  CurClient := i;
  Chan1Cmd[i] := Empty;
  Invset[j] := case | _ : Shrset[j];
}


transition recvReqe (i)
requires { Curcmd = Empty && Chan1Cmd[i] = Reqe }
{
  Curcmd := Reqe;
  CurClient := i;
  Chan1Cmd[i] := Empty;
  Invset[j] := case | _ : Shrset[j];
}

transition sendInv1 (i)
requires { Chan2Cmd[i] = Empty && Invset[i] = True && Curcmd = Reqe }
{
  Chan2Cmd[i] := Inv;
  Invset[i] := False;
}

transition sendInv2 (i)
requires { Chan2Cmd[i] = Empty && Invset[i] = True && Curcmd = Reqs &&
           Exgntd = True }
{
  Chan2Cmd[i] := Inv;
  Invset[i] := False;
}


transition sendInvack (i)
requires { Chan2Cmd[i] = Inv && Chan3Cmd[i] = Empty }
{
  Chan2Cmd[i] := Empty;
  Chan3Cmd[i] := Invack;
  CacheState[i] := Invalid;
}

transition recvInvack1 (i)
requires { Chan3Cmd[i] = Invack && Curcmd <> Empty } 
{
  Chan3Cmd[i] := Empty;
  Shrset[i] := False;
  Exgntd := False;
}

transition sendGnts (i)
requires { Curcmd = Reqs && CurClient = i && Chan2Cmd[i] = Empty && Exgntd = False }
{
  Chan2Cmd[i] := Gnts;
  Shrset[i] := True;
  Curcmd := Empty;
}

transition sendGnte (i)
requires { Curcmd = Reqe && CurClient = i && Chan2Cmd[i] = Empty && Exgntd = False &&
           Shrset[i] = False && forall_other j. Shrset[j] = False }
{
  Chan2Cmd[i] := Gnte;
  Shrset[i] := True;
  Curcmd := Empty;
  Exgntd := True;
}

transition recvGnts (i)
requires { Chan2Cmd[i] = Gnts }
{
  CacheState[i] := Shared;
  Chan2Cmd[i] := Empty;
}

transition recvGnts (i)
requires { Chan2Cmd[i] = Gnte }
{
  CacheState[i] := Exclusive;
  Chan2Cmd[i] := Empty;
}