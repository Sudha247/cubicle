
number_procs 2

type loc = L1 | L2 | L3 | CS | End

(* Program counters *)
array PC[proc] : loc

(* Global variables *)
weak var Want1 : int
weak var Want2 : int
weak var Turn : int

init (p) { PC[p] = L1 && Want1 = 0 && Want2 = 0 }

unsafe (p1 p2) { PC[p1] = CS && PC[p2] = CS }


(* Thread 1 *)

transition t1_L1_L2_W_Want1_True ()
requires { PC[#1] = L1 }
{ PC[#1] := L2; write(#1, Want1, 1) }

transition t1_L2_L3_W_Turn_p2 ()
requires { PC[#1] = L2 }
{ PC[#1] := L3; write(#1, Turn, 2) }

transition t1_L3_CS_R_Want2_False ()
requires { PC[#1] = L3 && read(#1, Want2) = 0 }
{ PC[#1] := CS }

transition t1_L3_CS_R_Turn_p1 ()
requires { PC[#1] = L3 && read(#1, Turn) = 1 }
{ PC[#1] := CS }

(* Critical section *)

transition t1_CS_L1_W_Want1_False ()
requires { PC[#1] = CS }
{ PC[#1] := L1; write(#1, Want1, 0) }


(* Thread 2 *)

transition t2_L1_L2_W_Want2_True ()
requires { PC[#2] = L1 }
{ PC[#2] := L2; write(#2, Want2, 1) }

transition t2_L2_L3_W_Turn_p1 ()
requires { PC[#2] = L2 }
{ PC[#2] := L3; write(#2, Turn, 1) }

transition t2_L3_CS_R_Want2_False ()
requires { PC[#2] = L3 && read(#2, Want1) = 0 }
{ PC[#2] := CS }

transition t2_L3_CS_R_Turn_p2 ()
requires { PC[#2] = L3 && read(#2, Turn) = 2 }
{ PC[#2] := CS }

(* Critical section *)

transition t2_CS_L1_W_Want2_False ()
requires { PC[#2] = CS }
{ PC[#2] := L1; write(#2, Want2, 0) }
