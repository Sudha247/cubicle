
number_procs 2

type loc = L1 | L2 | L3 | L4 | L5 | CS | End

(* Program counters *)
array PC[proc] : loc

(* Local variables *)
array LWant1[proc] : int
array LWant2[proc] : int
array LTurn[proc] : int

(* Global variables *)
weak var Want1 : int
weak var Want2 : int
weak var Turn : int

init (p) { PC[p] = L1 && Want1 = 0 && Want2 = 0 }

unsafe (p1 p2) { PC[p1] = CS && PC[p2] = CS }


(* Thread 1 *)

transition t1_L1_L2_W_Want1_True ()
requires { PC[#1] = L1 }
{ PC[#1] := L2; write(#1, Want1, 1) }

transition t1_L2_L3_W_Turn_p2 ()
requires { PC[#1] = L2 }
{ PC[#1] := L3; write(#1, Turn, 2) }

transition t1_L3_L4_R_Want2 ()
fences (#1)
requires { PC[#1] = L3 }
{ PC[#1] := L4; LWant2[#1] := read(#1, Want2) }

transition t1_L4_L5_R_Turn ()
requires { PC[#1] = L4 }
{ PC[#1] := L5; LTurn[#1] := read(#1, Turn) }

transition t1_L4_L3_Loop ()
requires { PC[#1] = L5 && LWant2[#1] = 1 && LTurn[#1] <> 1 }
{ PC[#1] := L3 }

transition t1_L5_CS_Want2_False ()
requires { PC[#1] = L5 && LWant2[#1] = 0 }
{ PC[#1] := CS }

transition t1_L5_CS_Turn_p1 ()
requires { PC[#1] = L5 && LTurn[#1] = 1 }
{ PC[#1] := CS }

(* Critical section *)

transition t1_CS_L1_W_Want1_False ()
requires { PC[#1] = CS }
{ PC[#1] := L1; write(#1, Want1, 0) }


(* Thread 2 *)

transition t2_L1_L2_W_Want2_True ()
requires { PC[#2] = L1 }
{ PC[#2] := L2; write(#2, Want2, 1) }

transition t2_L2_L3_W_Turn_p1 ()
requires { PC[#2] = L2 }
{ PC[#2] := L3; write(#2, Turn, 1) }

transition t2_L3_L4_R_Want1 ()
fences (#2)
requires { PC[#2] = L3 }
{ PC[#2] := L4; LWant1[#2] := read(#2, Want1) }

transition t2_L4_L5_R_Turn ()
requires { PC[#2] = L4 }
{ PC[#2] := L5; LTurn[#2] := read(#2, Turn) }

transition t2_L4_L3_Loop ()
requires { PC[#2] = L5 && LWant1[#2] = 1 && LTurn[#2] <> 2 }
{ PC[#2] := L3 }

transition t2_L5_CS_Want1_False ()
requires { PC[#2] = L5 && LWant1[#2] = 0 }
{ PC[#2] := CS }

transition t2_L5_CS_Turn_p2 ()
requires { PC[#2] = L5 && LTurn[#2] = 2 }
{ PC[#2] := CS }

(* Critical section *)

transition t2_CS_L1_W_Want2_False ()
requires { PC[#2] = CS }
{ PC[#2] := L1; write(#2, Want2, 0) }
