
type req = Empty | Reqs | Reqe
type cstate = Invalid | Shared | Exclusive

var Exgntd : bool
var Curcmd : req
var Curptr : proc

array Cache[proc] : cstate
array Shrset[proc] : bool
weak array Chan[proc] : req

init (z) { Cache[z] = Invalid && Shrset[z] = False &&
           Exgntd = False && Curcmd = Empty && Chan[z]=Empty }

unsafe (z1 z2) { Cache[z1] = Exclusive && Cache[z2] = Shared }

transition send_shared (n)
requires { n @ Chan[n] = Empty && Cache[n] = Invalid }
{
  n @ Chan[n] := Reqs
}

transition recv_shared (n)
requires { Curcmd = Empty && n @ Chan[n] = Reqs }
{ 	 
  Curcmd := Reqs; 
  Curptr := n ;
  n @ Chan[n] := Empty
}
    
transition send_exclusive (n)
requires { n @ Chan[n] = Empty && Cache[n] <> Exclusive }
{ 
  n @ Chan[n] := Reqe
}

transition recv_exclusive (n)
requires { Curcmd = Empty && n @ Chan[n] = Reqe }
{ 
  Curcmd := Reqe; 
  Curptr := n ;
  n @ Chan[n] := Empty 
}
    
transition inv_1 (n)
requires { Shrset[n]=True  &&  Curcmd = Reqe }
{ 
  Exgntd := False;
  Cache[j] := case 
  	       | j = n : Invalid 
	       | _ : Cache[j] ;
  Shrset[j] := case
  	        | j= n : False 
		| _ : Shrset[j] 
}

transition inv_2 (n)
requires { Shrset[n]=True  && Curcmd = Reqs && Exgntd=True }
{ 
  Exgntd := False;
  Cache[j] := case
	       | j = n : Invalid 
	       | _ : Cache[j] ;
  Shrset[j] := case
  	        | j= n : False 
		| _ : Shrset[j] 
}
    
transition gnt_shared (n)
requires { Curptr = n && Curcmd = Reqs && Exgntd = False }
{ 
  Curcmd := Empty;
  Shrset[j] := case 
  	       	| j = n : True 
		| _ : Shrset[j] ;
  Cache[j] := case 
  	       | j = n : Shared 
	       | _ : Cache[j] 
}

transition gnt_exclusive (n)
requires { Shrset[n] = False && Curcmd = Reqe && Exgntd = False && 
	  Curptr = n && forall_other l. Shrset[l] = False }
{ 
  Curcmd := Empty; 
  Exgntd := True;
  Shrset[j] := case 
  	        | j = n : True 
		| _ : Shrset[j] ;
  Cache[j] := case 
  	       | j = n : Exclusive 
	       | _ : Cache[j] 
}
