
(* Example 8-10 Stores Are not Reordered with Locks *)

number_procs 2

type loc = L1 | L2 | End

array PC[proc] : loc

array EAX[proc] : int
array EBX[proc] : int
array ECX[proc] : int

weak var X : int
weak var Y : int

init (p) {
    PC[p] = L1 &&
    EAX[p] = 1 && EBX[p] = 0 && ECX[p] = 0 &&
    X = 0 && Y = 0 }

(* FORBIDDEN Final State *)
unsafe (p1 p2) {
    p1 = #1 && p2 = #2 &&
    PC[p1] = End && PC[p2] = End &&
    EBX[p2] = 1 && ECX[p2] = 0 }

transition t1_1 (p)
requires { p = #1 && PC[p] = L1 }
{ PC[p] := L2; EAX[p] := read(p, X); write(p, X, EAX[p]) }

transition t1_2 (p)
requires { p = #1 && PC[p] = L2 }
{ PC[p] := End; write(p, Y, 1) }

transition t2_1 (p)
requires { p = #2 && PC[p] = L1 }
{ PC[p] := L2; EBX[p] := read(p, Y) }

transition t2_2 (p)
requires { p = #2 && PC[p] = L2 }
{ PC[p] := End; ECX[p] := read(p, X) }
