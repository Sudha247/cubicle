
(* Example 8-6 Stores Are Transitively Visible *)

number_procs 3

type loc = L1 | L2 | End

array PC[proc] : loc

array EAX[proc] : int
array EBX[proc] : int
array ECX[proc] : int

weak var X : int
weak var Y : int

init (p) {
    PC[p] = L1 &&
    EAX[p] = 0 && EBX[p] = 0 && ECX[p] = 0 &&
    X = 0 && Y = 0 }

(* FORBIDDEN Final State *)
unsafe (p1 p2 p3) {
    p1 = #1 && p2 = #2 && p3 = #3 &&
    PC[p1] = End && PC[p2] = End && PC[p3] = End &&
    EAX[p2] = 1 && EBX[p3] = 1 && ECX[p3] = 0 }

transition t1_1 (p)
requires { p = #1 && PC[p] = L1 }
{ PC[p] := End; write(p, X, 1) }

transition t2_1 (p)
requires { p = #2 && PC[p] = L1 }
{ PC[p] := L2; EAX[p] := read(p, X) }

transition t2_2 (p)
requires { p = #2 && PC[p] = L2 }
{ PC[p] := End; write(p, Y, 1) }

transition t3_1 (p)
requires { p = #3 && PC[p] = L1 }
{ PC[p] := L2; EBX[p] := read(p, Y) }

transition t3_2 (p)
requires { p = #3 && PC[p] = L2 }
{ PC[p] := End; ECX[p] := read(p, X) }
