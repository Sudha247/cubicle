
(* Example 8-9 Loads Are not Reordered with Locks *)

number_procs 2

type loc = L1 | L2 | End

array PC[proc] : loc

array EAX[proc] : int
array EBX[proc] : int
array ECX[proc] : int
array EDX[proc] : int

weak var X : int
weak var Y : int

init (p) {
    PC[p] = L1 &&
    EAX[p] = 1 && EBX[p] = 0 && ECX[p] = 1 && EDX[p] = 0 &&
    X = 0 && Y = 0 }

(* FORBIDDEN Final State *)
unsafe (p1 p2) {
    p1 = #1 && p2 = #2 &&
    PC[p1] = End && PC[p2] = End &&
    EAX[p1] = 0 && EBX[p1] = 0 && ECX[p2] = 0 && EDX[p2] = 0 }

transition t1_1 (p)
requires { p = #1 && PC[p] = L1 }
{ PC[p] := L2; EAX[p] := read(p, X); write(p, X, EAX[p]) }

transition t1_2 (p)
requires { p = #1 && PC[p] = L2 }
{ PC[p] := End; EBX[p] := read(p, Y) }

transition t2_1 (p)
requires { p = #2 && PC[p] = L1 }
{ PC[p] := L2; ECX[p] := read(p, Y); write(p, Y, ECX[p]) }

transition t2_2 (p)
requires { p = #2 && PC[p] = L2 }
{ PC[p] := End; EDX[p] := read(p, X) }
